
model {

  # Priors and linear models
  for (s in 1:n.site){
    for (t in 1:(n.occ-1)){
      
      # phi: probability of survival and return
      # here it is constant across year t and site s
      phi[t, s] <- ilogit(lphi_intercept) # survival
      lphi[t, s] <- lphi_intercept
      
      # p: recapture probability given return
      # here, it is a constant across year t and site s
      p[t, s] <- p_intercept # p_year[t]
      
    }
  }
  
  # Hyperpriors for hyperparams
  
  # fixed effect for yearly survival and recapture
  p_intercept ~ dunif(0,1)
  phi_intercept ~ dunif(0,1)
  lphi_intercept<-logit(phi_intercept)

  # Multinomial likelihood for the m-array data (JAGS style)
  for (s in 1:n.site){
    for (t in 1:(n.occ-1)){
      MARR[t,1:n.occ,s] ~ dmulti(pr[t, , s], R[t,s])
    }
  }

  # Define the cell probabilities of the m-array

  for (s in 1:n.site){
    for (t in 1:(n.occ-1)){
      # Main diagonal
      q[t,s] <- 1-p[t,s] # Probability of non-recapture
      pr[t,t,s] <- phi[t,s]*p[t,s]
      # Above main diagonal
      for (j in (t+1):(n.occ-1)){
        pr[t,j,s] <- prod(phi[t:j,s])*prod(q[t:(j-1),s])*p[j,s]
      } #j
      # Below main diagonal
      for (j in 1:(t-1)){
        pr[t,j,s] <- 0
      }
    }
  }

  # Last column of m-array: probability of non-recapture
  for (s in 1:n.site){
    for (t in 1:(n.occ-1)){
      pr[t,n.occ,s] <- 1-sum(pr[t,1:(n.occ-1),s])
    }
  }
}
